def gitCommitHash = {
    def cmd = "git rev-parse --short HEAD"
    try {
        def ver = cmd.execute().text.trim()
        if (ver == "") return ""
        return "+" + ver
    } catch (Exception e) {
        e.printStackTrace()
        return ""
    }
}

allprojects {
    repositories {
        mavenCentral()
        maven {
            name = 'Jitpack'
            url = 'https://jitpack.io'
        }
        maven {
            name = "TerraformersMC"
            url = "https://maven.terraformersmc.com/releases/"
        }
    }
}

subprojects {
    apply plugin: 'java-library'

    group = "me.zhenxin"
    version = "3.0.0-dev" + gitCommitHash()

    dependencies {
        compileOnly("org.projectlombok:lombok:1.18.26")
        annotationProcessor("org.projectlombok:lombok:1.18.26")
    }

    jar {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        from {
            //noinspection GroovyMissingReturnStatement
            configurations.runtimeClasspath.collect {
                if (project.name != "zmusic-core") {
                    if (it.name.contains("zmusic-core")) {
                        it.isDirectory() ? it : zipTree(it)
                    }
                }
            }
        }
        from("${rootDir}/LICENSE")
    }

    processResources {
        inputs.property "version", project.version

        filesMatching("fabric.mod.json") {
            expand "version": project.version
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
    }
}